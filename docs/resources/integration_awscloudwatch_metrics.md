---
page_title: "Resource eventstorecloud_integration_awscloudwatch_metrics - terraform-provider-eventstorecloud"
subcategory: ""
description: |-
  Manages AwsCloudWatch integration sink resources
---

# Resource (eventstorecloud_integration_awscloudwatch_metrics)

Manages AwsCloudWatch integration sinks.

**NOTE**: This functionality is currently in beta. To access it please contact support.

## Example Usage

```terraform

locals {
  metric_namespace        = "esdb_metrics_${var.stage}"
}

resource "aws_iam_access_key" "esdb_metrics" {
  user = aws_iam_user.esdb_metrics.name
}

resource "aws_iam_user" "esdb_metrics" {
  name = "esdb_metrics_user-${var.stage}"
  path = "/esdb_metrics/"
  # TODO: make path use `stage`?
}

resource "aws_iam_user_policy" "esdb_metrics" {
  name = "esdb_metrics_user_policy-${var.stage}"
  user = aws_iam_user.esdb_metrics.name

  # TODO: restrict to just the created group
  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": [
        "cloudwatch:PutMetricData"        
      ],
      "Effect": "Allow",
      "Resource": "*",
      "Condition": {
          "ForAnyValue:StringEqualsIgnoreCase": {
                "cloudwatch:namespace": [
                    "${local.metric_namespace}",
                    "${local.metric_namespace}/eventstoredb",
                    "${local.metric_namespace}/host"
                ]
            }
        }
    }        
  ]
}
EOF
}


resource "eventstorecloud_integration_awscloudwatch_metrics" "cloudwatch"{
  project_id        = var.project_id
  cluster_ids       = [ var.cluster_id ]
  description       = "send ESDB metrics to AWS CloudWatch"
  access_key_id     = aws_iam_access_key.esdb_metrics.id
  secret_access_key = aws_iam_access_key.esdb_metrics.secret  
  region            = var.region
  namespace         = local.metric_namespace
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- **access_key_id** (String) The access key ID of IAM credentials which have permissions to create and write to the log group
- **cluster_ids** (List) List of cluster IDs to which the integration applies
- **description** (String) Human readable description of the integration
- **namespace** (String) The name of the metric namespace
- **project_id** (String) ID of the project to which the integration applies
- **region** (String) The AWS region name of the log group
- **secret_access_key** (String) The secret access key of IAM credentials which will be used to write to the log groups
- **source** (String) The source of the AwsCloudWatch integration sink (currently this may only be "metrics")

## Import

Import is supported using the following syntax:

```shell
terraform import eventstorecloud_integration_awscloudwatch_metrics.cloudwatch project_id:integration_id
```

## IAM Credentials and Security Implications

It is recommended you create credentials especially for use with this resource which have extremely limited access. A good example is shown in the snippet above, where the `aws_iam_user` resource only has permissions to put metric data into the given AWS CloudWatch namespace.

While is it possible to use the `eventstorecloud_integration` resource with a sink property of `awsCloudWatchMetrics`, it is recommended to use the `eventstorecloud_integration_awscloudwatch_metrics` resource instead as the IAM credentials get marked as sensitive to Terraform and will not be shown when running steps such as `terraform plan`. 

Even then, the IAM credentials given to this resource will be stored in the Terraform raw state as plain-text. More information on sensitive data in Terraform state can be read [here](https://www.terraform.io/language/state/sensitive-data).
