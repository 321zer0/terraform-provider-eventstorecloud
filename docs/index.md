---
layout: ""
page_title: "Provider: Event Store Cloud"
description: |-
  Terraform provider for Event Store Cloud
---

# Event Store Cloud Provider

This provider can be used for provisioning resources in [Event Store Cloud][esc].

# Installation

## Terraform 0.12

We provide binary releases for macOS, Windows and Linux via GitHub releases. In order for Terraform to find the plugin, the appropriate binary must be placed into the Terraform third-party plugin directory, the location of which varies by operating system:

- `%APPDATA%\terraform.d\plugins` on Windows
- `~/.terraform.d/plugins` on macOS or Linux

Alternatively, the binary can be placed alongside the main `terraform` binary.

On macOS and Linux, you can download the provider using the following commands:

- macOS: `curl -o ./terraform-provider-eventstorecloud.zip -L https://github.com/EventStore/terraform-provider-eventstorecloud/releases/download/v1.5.3/terraform-provider-eventstorecloud_1.5.3_darwin_amd64.zip && unzip ./terraform-provider-eventstorecloud.zip && mv ./terraform-provider-eventstorecloud_v1.5.3 ~/.terraform.d/plugins/terraform-provider-eventstorecloud_v1.5.3`
- Linux: `curl -o ./terraform-provider-eventstorecloud.zip -L https://github.com/EventStore/terraform-provider-eventstorecloud/releases/download/v1.5.3/terraform-provider-eventstorecloud_1.5.3_linux_amd64.zip && unzip ./terraform-provider-eventstorecloud.zip && mv ./terraform-provider-eventstorecloud_v1.5.3 ~/.terraform.d/plugins/terraform-provider-eventstorecloud_v1.5.3`

If you prefer to install from source, use the `make install` target in this repository. You'll need a Go 1.13+
development environment.

## Terraform 0.13+

Terraform now supports third party modules installed via the plugin registry. Add the following to your terraform module
configuration.

```
terraform {
  required_providers {
    eventstorecloud = {
      source = "EventStore/eventstorecloud"
      version = "1.5.3"
    }
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- **organization_id** (String)
- **token** (String)
- **token_store** (String)
- **url** (String)

## Example Usage

```terraform
variable "peering_account_id" {
  type = string
}

variable "peering_network_id" {
  type = string
}

variable "peering_route" {
  type = string
}

provider "eventstorecloud" {

}

resource "eventstorecloud_project" "chicken_window" {
  name = "Improved Chicken Window"
}

resource "eventstorecloud_network" "chicken_window" {
  name = "Chicken Window Net"

  project_id = eventstorecloud_project.chicken_window.id

  resource_provider = "aws"
  region            = "us-west-2"
  cidr_block        = "172.21.0.0/16"
}

resource "eventstorecloud_peering" "peering" {
  name = "Example Peering"

  project_id = eventstorecloud_network.chicken_window.project_id
  network_id = eventstorecloud_network.chicken_window.id

  peer_resource_provider = eventstorecloud_network.chicken_window.resource_provider
  peer_network_region    = eventstorecloud_network.chicken_window.region

  peer_account_id = var.peering_account_id
  peer_network_id = var.peering_network_id
  routes          = [var.peering_route]
}

resource "eventstorecloud_managed_cluster" "wings" {
  name = "Wings Cluster"

  project_id = eventstorecloud_network.chicken_window.project_id
  network_id = eventstorecloud_network.chicken_window.id

  topology       = "single-node"
  instance_type  = "F1"
  disk_size      = 16
  disk_type      = "gp2"
  server_version = "20.6"
}

output "chicken_window_id" {
  value = eventstorecloud_project.chicken_window.id
}

output "chicken_window_net" {
  value = eventstorecloud_network.chicken_window
}

output "chicken_window_peering" {
  value = eventstorecloud_peering.peering
}

output "wings_cluster_dns_name" {
  value = eventstorecloud_managed_cluster.wings.dns_name
}
```

[terraform]: (https://terraform.io)
